;license:MIT
;(c) 2022 by 4am
;
; utility functions
;

; - CalculateRowBase
; - SwapXY
; - LogicalColumnToPhysicalColumn
; - PhysicalColumnToLogicalColumn

;------------------------------------------------------------------------------
; CalculateRowBase
; calculate address offset within a board buffer
;
; in:    X = logical row number (0x00..0x0F)
; out:   A = low byte of address of board buffer for this row
;------------------------------------------------------------------------------
kRowBases
         !byte $00,$10,$20,$30,$40,$50,$60,$70,$80,$90,$A0,$B0,$C0,$D0,$E0,$F0
!macro CalculateRowBase {
         lda   kRowBases, x
}

;------------------------------------------------------------------------------
; SwapXY
; transfer X register to Y and Y register to X
;
; in:    none
; out:   $tmpx, $tmpy clobbered
;------------------------------------------------------------------------------
!macro SwapXYInline {
         stx   tmpx
         sty   tmpy
         ldx   tmpy
         ldy   tmpx
}
SwapXY
         +SwapXYInline
         rts

;------------------------------------------------------------------------------
; LogicalColumnToPhysicalColumn
;
; in:    Y = logical column number
; out:   Y = physical byte offset to use against an HGR base address
;        A clobbered
;        X preserved
;------------------------------------------------------------------------------
LogicalColumnToPhysicalColumn
         iny
         iny
         tya
         asl
         tay
         rts

;------------------------------------------------------------------------------
; PhysicalColumnToLogicalColumn
;
; in:    Y = physical byte offset to use against an HGR base address
; out:   Y = logical column number
;        A clobbered
;        X preserved
;------------------------------------------------------------------------------
PhysicalColumnToLogicalColumn
         tya
         lsr
         tay
         dey
         dey
         rts
