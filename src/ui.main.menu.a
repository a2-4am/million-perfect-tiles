;license:MIT
;(c) 2022 by 4am
;
; main menu screen display and event loop
;

; Codes returned by event handlers
kStayOnMainMenu = 0
kQuit = 1

kMainMenuKeys                        ; must keep in sync with kMainMenuKeyHandlersLo/Hi arrays
                                     ; except for last byte ($00) which doesn't need an associated handler
         !byte $8D                   ; Return
         !byte $D0                   ; P
         !byte $F0                   ; p
         !byte $C4                   ; D
         !byte $E4                   ; D
         !byte $C6                   ; F
         !byte $E6                   ; f
         !byte $D1                   ; Q
         !byte $F1                   ; q
         !byte $9B                   ; Esc
         !byte $00

kMainMenuKeyHandlersLo
         !byte <MainMenuEventReturn
         !byte <MainMenuEventP
         !byte <MainMenuEventP
         !byte <MainMenuEventD
         !byte <MainMenuEventD
         !byte <MainMenuEventF
         !byte <MainMenuEventF
         !byte <MainMenuEventQ
         !byte <MainMenuEventQ
         !byte <MainMenuEventQ

kMainMenuKeyHandlersHi
         !byte >MainMenuEventReturn
         !byte >MainMenuEventP
         !byte >MainMenuEventP
         !byte >MainMenuEventD
         !byte >MainMenuEventD
         !byte >MainMenuEventF
         !byte >MainMenuEventF
         !byte >MainMenuEventQ
         !byte >MainMenuEventQ
         !byte >MainMenuEventQ

; len(kMainMenuVTABs) == len(kMainMenuHTABs) == len(kMainMenuStringsLo) == len(kMainMenuStringsHi)
; or you will regret this
kMainMenuVTABs
         !byte 1,2,3,4,5,6,7,9,10,11,12,13,14
kMainMenuHTABs
         !byte 6,2,2,2,5,5,6,5,5,5,5,5,5
kMainMenuStringsLo
         !byte <sMainMenuLine1
         !byte <sMainMenuLine2
         !byte <sMainMenuLine3
         !byte <sMainMenuLine4
         !byte <sMainMenuLine5
         !byte <sMainMenuLine6
         !byte <sMainMenuLine7
         !byte <sMainMenuLine8
         ; nothing on line 9
         !byte <sMainMenuLine10
         !byte <sMainMenuLine11
         !byte <sMainMenuLine12
         !byte <sMainMenuLine13
         !byte <sMainMenuLine14
kMainMenuStringsHi
         !byte >sMainMenuLine1
         !byte >sMainMenuLine2
         !byte >sMainMenuLine3
         !byte >sMainMenuLine4
         !byte >sMainMenuLine5
         !byte >sMainMenuLine6
         !byte >sMainMenuLine7
         !byte >sMainMenuLine8
         ; nothing on line 9
         !byte >sMainMenuLine10
         !byte >sMainMenuLine11
         !byte >sMainMenuLine12
         !byte >sMainMenuLine13
         !byte >sMainMenuLine14

MainMenuEventLoop
         jsr   DrawMainMenuBackground
         jsr   DrawMainMenuText
         bit   GFXMODE
         +DEBUGWAIT
         jsr   SelectSizeEventLoop
         jmp   $ff59

MainMenuEventReturn
MainMenuEventP
MainMenuEventD
MainMenuEventF
MainMenuEventQ

DrawMainMenuBackground
         lda   #$10
         sta   logical_board_size
         lda   #>kTitleBoard
         sta   board_base_ptr+1
OneTimeLoad
         jsr   LoadRandomBoardForTitle ; SMC opcode
AutoSolve
         bit   TEXTMODE
         jsr   DrawEntireBoard
@loop    jsr   FindValidMove
         bcc   @draw
         jsr   TransposeBoard
         jsr   FindValidMove
         jsr   TransposeBoard
         bcs   @exit
         stx   tmpx
         sty   tmpy
         ldx   tmpy
         ldy   tmpx
@draw
         bit   GFXMODE
         jsr   DrawLargeCharacter
         pha
         jsr   CalculateRowBase
         sta   board_base_ptr
         pla
         sta   (board_base_ptr), y
         bne   @loop                 ; always branches
@exit
         rts

LoadRandomBoardForTitle
         lda   RNDSEED+1
         and   #1
         tay
         lda   RNDSEED
         ldx   #>kTitleBoard
         jsr   nth
         lda   #$2C       ; BIT opcode
         sta   OneTimeLoad
         rts

DrawMainMenuText
         lda   #12
         sta   row1
-        ldy   row1
         lda   kMainMenuVTABs, y
         sta   VTAB
         lda   kMainMenuHTABs, y
         tax
         lda   kMainMenuStringsLo, y
         pha
         lda   kMainMenuStringsHi, y
         tay
         pla
         jsr   DrawLargeString
         dec   row1
         bpl   -
         rts
