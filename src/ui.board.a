;license:MIT
;(c) 2022 by 4am
;
; drawing routines for the puzzle board
;

; DrawBoardForTitle
; DrawBoardForUser
; DrawBoardAt

DrawBoardForTitle
         ldx   #>kTitleBoardBuffer
         +HIDE_NEXT_2_BYTES
         ; /!\ execution falls through here to DrawBoardForUser
DrawBoardForUser
         ldx   #>kUserBoardBuffer
         ; /!\ execution falls through here to DrawBoardAt
DrawBoardAt
; in:    X = high byte of page-aligned board buffer
; out:   all flags and registers clobbered
         stx   board_base_ptr+1
         stx   @a+2
         lda   #$FF
         sta   @a+1
         ldx   #$0F
-        ldy   #$0F
@a       lda   $FDFD                 ; SMC
         and   #$7F
         cmp   #$02
         beq   @draw
         cmp   #$03
         bne   @nodraw
@draw    jsr   DrawLargeCharacter
@nodraw  dec   @a+1
         dey
         bpl   @a
         dex
         bpl   -
         rts
