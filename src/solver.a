;license:MIT
;(c) 2023 by 4am
;
; solving routines
;
; - FindInvalidMoveForUser
; - FindValidMoveForUser
; - FindValidMove
; - TransposeBoardIntoBackupBuffer
; - FindEmptyCoordinatesForUser

kTransposeFocusTypes
         !byte kFocusEntireColumn, kFocusEntireRow, kFocus1x1, kFocus3x1, kFocus1x3

;------------------------------------------------------------------------------
; FindInvalidMoveForUser
; find a tile, row, or column that does not adhere to game rules
;
; in:    $kUserBoardBuffer contains game board
;        gLastSelectedBoardSize contains logical board size (6,8,10,12,14,16)
; out:   C = 0 if found invalid move, then
;          X = tile row (0x00..0x0F)
;          Y = tile column (0x00..0x0F)
;        extended information is available in zero page to help the caller
;        draw a focus rectangle:
;          $extended_message
;          $extended_x
;          $extended_y
;          $extended_type
;        C = 1 if no invalid move found
;------------------------------------------------------------------------------
FindInvalidMoveForUser
         ldx   #>kUserBoardBuffer
         jsr   TransposeBoardIntoBackupBuffer
         ldy   gLastSelectedBoardSize
         sty   logical_board_size

         ldx   #>kUserBoardBuffer
         jsr   FindInvalidRule1
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindInvalidRule1
         bcc   foundInTransposed

         ldx   #>kUserBoardBuffer
         jsr   FindInvalidRule2
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindInvalidRule2
         bcc   foundInTransposed

         ldx   #>kUserBoardBuffer
         jsr   FindInvalidRule3
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindInvalidRule3
         bcc   foundInTransposed
         ; /!\ execution falls through here, but with C=1
@foundInOriginal
         rts

;------------------------------------------------------------------------------
; FindValidMoveForUser
; find empty coordinates on user board where a tile could be placed
; in accordance with game rules, based on current board state
;
; in:    $kUserBoardBuffer contains game board
;        gLastSelectedBoardSize contains logical board size (6,8,10,12,14,16)
; out:   see FindValidMove
;------------------------------------------------------------------------------
FindValidMoveForUser
         ldx   #>kUserBoardBuffer
         stx   board_base_ptr_original
         jsr   TransposeBoardIntoBackupBuffer
         lda   #$48                  ; PHA opcode
         sta   FindValidRule1_SetExtendedInfo_Patch
         ldy   #$00
         sty   start_y
         ldy   gLastSelectedBoardSize
         sty   logical_board_size
         ; /!\ execution falls through here to FindValidMove

;------------------------------------------------------------------------------
; FindValidMove
; find empty coordinates where a tile could be placed in accordance with
; game rules, based on current board state
;
; in:    $board_base_ptr_original = high byte of board buffer
;        $logical_board_size = logical board size (6,8,10,12,14,16)
;        $kTransposedBoardBuffer contains a copy of the board buffer but transposed
; out:   C = 0 if found valid move, then
;          A = tile value (0x02 or 0x03)
;          X = tile row (0x00..0x0F)
;          Y = tile column (0x00..0x0F)
;        extended information is available in zero page to help the caller
;        draw a focus rectangle:
;          $extended_message
;          $extended_x
;          $extended_y
;          $extended_type
;        C = 1 if no valid move found
;------------------------------------------------------------------------------
FindValidMove
         ldx   board_base_ptr_original
         jsr   FindValidRule1
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindValidRule1
         bcc   foundInTransposed

         ldx   board_base_ptr_original
         jsr   FindValidRule2
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindValidRule2
         bcc   foundInTransposed

         ldx   board_base_ptr_original
         jsr   FindValidRule3
         bcc   @foundInOriginal

         ldx   #>kTransposedBoardBuffer
         jsr   FindValidRule3
         bcc   foundInTransposed
         ; /!\ execution falls through here, but with C=1
@foundInOriginal
         rts
foundInTransposed
         stx   tmpx
         sty   tmpy
         ldy   extended_x
         ldx   extended_y
         stx   extended_x
         sty   extended_y
         ldx   extended_type
         ldy   kTransposeFocusTypes, x
         sty   extended_type
         ldx   tmpy                  ; X/Y swapped
         ldy   tmpx
         rts

;------------------------------------------------------------------------------
; TransposeBoardIntoBackupBuffer
; transpose game board into kTransposedBoardBuffer (like a matrix, so row N
; becomes column N and column M becomes row M)
;
; in:    X = high byte of page-aligned board buffer
; out:   all registers & flags clobbered
;        $tmpy clobbered
;------------------------------------------------------------------------------
TransposeBoardIntoBackupBuffer
         stx   @a+2
         stx   @b+2
         ldx   #$00
         ldy   #$00
@a       lda   $FD00, y              ; SMC
         pha
@b       lda   $FD00, x              ; SMC
         sta   kTransposedBoardBuffer, y
         pla
         sta   kTransposedBoardBuffer, x
         txa
         clc
         adc   #$10
         tax
         bcc   +
         iny
         beq   ++
         sty   tmpy
         inx
         txa
         clc
         adc   tmpy
         tax
         tay
         dey
+        iny
         bne   @a
++       rts

;------------------------------------------------------------------------------
; FindEmptyCoordinatesForUser
; find the top-left-most board coordinates with no tile
;
; in:    none
; out:   C = 0 if found empty coordinates, then
;          X = logical row (0x00..0x0F)
;          Y = logical column (0x00..0x0F)
;        C = 1 if no empty coordinates found (board is full)
;------------------------------------------------------------------------------
FindEmptyCoordinatesForUser
         ldx   #>kUserBoardBuffer
         stx   board_base_ptr+1
         ldy   #0
         sty   board_base_ptr
-        lda   (board_base_ptr), y
         beq   YIndexToXYCoordinates
         iny
         bne   -
         sec
         rts
YIndexToXYCoordinates
         tya
         lsr
         lsr
         lsr
         lsr
         tax
         tya
         and   #$0F
         tay
         clc
         rts

;------------------------------------------------------------------------------
; private

;------------------------------------------------------------------------------
; FindInvalidRule1
; find a tile in any row that violates the no-3-in-a-row rule
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if tile was found (+ all the things listed in FindInvalidMove)
;        C = 1 if no tile was found
;------------------------------------------------------------------------------
FindInvalidRule1
         stx   board_base_ptr+1
         stx   board_base_shift_plus_1+1
         stx   board_base_shift_plus_2+1
         ldy   #$00
         sty   board_base_ptr
@loop
         lda   maybe_0xx, y
         bne   @next_tile
         lda   (board_base_ptr), y
         cmp   #kPadding
         bcs   @next_tile
         and   #TILE_MASK
         bne   @found_nonempty
@next_tile
         iny
         bne   @loop
@no_tile_found
         sec
         rts
@found_nonempty
         sta   tile_a
         lda   (board_base_shift_plus_1), y
         and   #TILE_MASK
         cmp   tile_a
         bne   @next_tile
         lda   (board_base_shift_plus_2), y
         and   #TILE_MASK
         cmp   tile_a
         bne   @next_tile
         ldx   #$00
         lda   (board_base_ptr), y
         bpl   @tile_found
         dex
         iny
         lda   (board_base_ptr), y
         bpl   @tile_found
         dex
         iny
@tile_found
         stx   y_shift
         sty   start_y
         jsr   YIndexToXYCoordinates
         lda   #<sNever
         sta   extended_message
         lda   #>sNever
         sta   extended_message+1
         lda   #kFocus1x3
         sta   extended_type
         stx   extended_x
         tya
         ;C=0 here (set in YIndexToXYCoordinates)
         adc   y_shift
         sta   extended_y
         lda   #kEmptyTile
         clc
         rts

;------------------------------------------------------------------------------
; FindInvalidRule2
; find a row that violates the equal-colors-in-each-row rule
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if row was found (+ all the things listed in FindInvalidMove)
;        C = 1 if no row was found
;------------------------------------------------------------------------------
FindInvalidRule2
         stx   board_base_ptr+1
         ldx   #$0F                  ; tile row
@CheckRow
         +CalculateRowBase
         sta   board_base_ptr
         jsr   CountColorsInRow
         lda   colorcount2
         asl
         cmp   logical_board_size
         beq   +
         bcs   @tooMany
+        lda   colorcount3
         asl
         cmp   logical_board_size
         beq   +
         bcs   @tooMany
+        dex
         bpl   @CheckRow
         sec
         rts
@tooMany
         lda   #<sSame
         sta   extended_message
         lda   #>sSame
         sta   extended_message+1
         lda   #kFocusEntireRow
         sta   extended_type
         stx   extended_x
         ldy   #$0F
-        lda   (board_base_ptr), y
         bpl   +
         dey
         bpl   -
+        clc
         rts

;------------------------------------------------------------------------------
; FindInvalidRule3
; find a row that violates the every-row-is-unique rule
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if row was found (+ all the things listed in FindInvalidMove)
;        C = 1 if no row was found
;------------------------------------------------------------------------------
FindInvalidRule3
         stx   board_base_ptr+1
         stx   board_base_ptr2+1
         jsr   CountEmptyPerRow
@row1loop
         ldx   row1
         lda   row_empty_counts, x
         bne   @nextrow1
         ; found a full row
         +CalculateRowBase
         sta   board_base_ptr
         lda   #$0F
         sta   row2
@row2loop
         ldx   row2
         lda   row_empty_counts, x
         bne   @nextrow2
         ; found another full row
         +CalculateRowBase
         sta   board_base_ptr2
         ldx   #$00
         ldy   #$0F
@comparerowsloop
         lda   (board_base_ptr2), y
         cmp   #kPadding
         bcs   +
         and   #TILE_MASK
         sta   tmpx
         lda   (board_base_ptr), y
         cmp   #kPadding
         bcs   +
         and   #TILE_MASK
         cmp   tmpx
         bne   @nextrow2
         inx
+        dey
         bpl   @comparerowsloop
         txa
         bne   @foundDuplicate
@nextrow2
         dec   row2
         lda   row1
         cmp   row2
         bcc   @row2loop
@nextrow1
         dec   row1
         bpl   @row1loop
         sec
         rts
@foundDuplicate
         lda   #<sUnique
         sta   extended_message
         lda   #>sUnique
         sta   extended_message+1
         lda   #kFocusEntireRow
         sta   extended_type
         ldx   row1
         stx   extended_x
         ldy   #$0F
-        lda   (board_base_ptr), y
         bpl   +
         dey
         bpl   -
+        clc
         rts

;------------------------------------------------------------------------------
; FindValidRule1
; find a tile in any row that must be a certain color due to the
; no-3-in-a-row rule
;
; This routine is performance-sensitive since it is called so many times while
; auto-solving the title board at startup. Cycle-count all modifications.
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if tile was found (+ all the things listed in FindValidMove)
;        C = 1 if no tile was found
;------------------------------------------------------------------------------
FindValidRule1
         stx   board_base_ptr+1
         stx   board_base_shift_plus_1+1
         stx   board_base_shift_plus_2+1
         dex
         stx   board_base_shift_minus_1+1
         stx   board_base_shift_minus_2+1
         ldx   #$00
         stx   board_base_ptr
         ldy   start_y
@loop
         dey
         lda   (board_base_ptr), y
         beq   @found_empty
@next_tile
         dex
         bne   @loop
@no_tile_found
         sec
         rts
@found_empty
         lda   maybe_xx0, y
         bne   @not_xx0
         lda   (board_base_shift_minus_1), y
         beq   @not_x0x                         ; can skip x0x section too
         and   #TILE_MASK
         sta   tile_a
         lda   (board_base_shift_minus_2), y
         and   #TILE_MASK
         cmp   tile_a
         beq   @tile_found_shift_minus_2
@not_xx0
         lda   maybe_x0x, y
         bne   @not_x0x
         lda   (board_base_shift_plus_1), y
         beq   @next_tile                       ; can skip 0xx section too
         and   #TILE_MASK
         sta   tile_a
         lda   (board_base_shift_minus_1), y
         and   #TILE_MASK
         cmp   tile_a
         beq   @tile_found_shift_minus_1
@not_x0x
         lda   maybe_0xx, y
         bne   @next_tile
         lda   (board_base_shift_plus_1), y
         beq   @next_tile
         and   #TILE_MASK
         sta   tile_a
         lda   (board_base_shift_plus_2), y
         and   #TILE_MASK
         cmp   tile_a
         bne   @next_tile
         ldx   #$00
         beq   @tile_found           ; always branches
@tile_found_shift_minus_2
         ldx   #$FE
         bne   @tile_found           ; always branches
@tile_found_shift_minus_1
         ldx   #$FF
@tile_found
         stx   y_shift
         sty   start_y
         eor   #(kWhiteTile XOR kBlueTile)
         pha
         jsr   YIndexToXYCoordinates
         pla
FindValidRule1_SetExtendedInfo_Patch
         pha                         ; SMC
         lda   #<sNever
         sta   extended_message
         lda   #>sNever
         sta   extended_message+1
         lda   #kFocus1x3
         sta   extended_type
         stx   extended_x
         tya
         ;C=0 here (set in YIndexToXYCoordinates)
         adc   y_shift
         sta   extended_y
         pla
         clc
         rts

;------------------------------------------------------------------------------
; FindValidRule2
; find a tile in any row that must be a certain color due to the
; equal-colors-in-each-row rule
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if tile was found (+ all the things listed in FindValidMove)
;        C = 1 if no tile was found
;------------------------------------------------------------------------------
FindValidRule2
         stx   board_base_ptr+1
         ldx   #$0F                  ; tile row
@CheckRow
         +CalculateRowBase
         sta   board_base_ptr
         jsr   CountColorsInRow
         lda   colorcount2
         cmp   colorcount3
         beq   @no                   ; if color counts are the same, we're done (and the answer is no)
         asl
         cmp   logical_board_size
         beq   @fullOf2
         lda   colorcount3
         asl
         cmp   logical_board_size
         beq   @fullOf3
@no
         dex
         bpl   @CheckRow
         sec
         rts
@fullOf2
         lda   #kBlueTile
         +HIDE_NEXT_2_BYTES
@fullOf3
         lda   #kWhiteTile
         pha
         ldy   #$0F
-        lda   (board_base_ptr), y
         beq   @yes
         dey
         bpl   -                     ; always branches
@yes
         lda   #<sSame
         sta   extended_message
         lda   #>sSame
         sta   extended_message+1
         lda   #kFocusEntireRow
         sta   extended_type
         stx   extended_x
         sty   extended_y
         pla                         ; A=tile value, X/Y = row/column
         clc
         rts

;------------------------------------------------------------------------------
; FindValidRule3
; find a tile in any row that must be a certain color due to the
; no-duplicate-rows rule
;
; in:    X = high byte of page-aligned board buffer
; out:   C = 0 if tile was found (+ all the things listed in FindValidMove)
;        C = 1 if no tile was found
;------------------------------------------------------------------------------
FindValidRule3
         stx   board_base_ptr+1
         stx   board_base_ptr2+1
         jsr   CountEmptyPerRow
@row1loop
         ldx   row1
         lda   row_empty_counts, x
         bne   @nextrow1
         ; found a full row
         +CalculateRowBase
         sta   board_base_ptr
         lda   #$0F
         sta   row2
@row2loop
         ldx   row2
         lda   row_empty_counts, x
         cmp   #$02
         bne   @nextrow2
         ; found a row with 2 empty tiles
         stx   extended_x
         +CalculateRowBase
         sta   board_base_ptr2
         ldy   #$0F
@comparerowsloop
         lda   (board_base_ptr2), y
         beq   +
         cmp   #kPadding
         bcs   +
         and   #TILE_MASK
         sta   tmpx
         lda   (board_base_ptr), y
         and   #TILE_MASK
         cmp   tmpx
         bne   @nextrow2
+        dey
         bpl   @comparerowsloop
         ; row with 2 empty tiles is identical to the full row (except for the 2 empty tiles)
         ; so we can fill one of the empty tiles with the opposite of that column in the full row
         ; since no two rows can be completely identical
         ldy   #$0F
@findemptyloop
         lda   (board_base_ptr2), y
         bne   @nextempty
         lda   (board_base_ptr), y
         and   #TILE_MASK
         cmp   #kWhiteTile
         beq   @make_it_blue
         lda   #kWhiteTile
         +HIDE_NEXT_2_BYTES
@make_it_blue
         lda   #kBlueTile
         pha
         lda   #<sUnique
         sta   extended_message
         lda   #>sUnique
         sta   extended_message+1
         lda   #kFocusEntireRow
         sta   extended_type
         sty   extended_y
         pla
         clc
         rts
@nextempty
         dey
         bpl   @findemptyloop
@nextrow2
         dec   row2
         lda   row1
         cmp   row2
         bcc   @row2loop
@nextrow1
         dec   row1
         bpl   @row1loop
         sec
         rts

;------------------------------------------------------------------------------
; CountColorsInRow
; count white and blue tiles in one row
;
; in:    (board_base_ptr) points to a specific row in a game board
; out:   $colorcount2 (white), $colorcount3 (blue) populated
;        preserves X
;        clobbers A/Y/flags
;------------------------------------------------------------------------------
CountColorsInRow
         lda   #$00
         sta   colorcount2
         sta   colorcount3
         ldy   #$0F
-        lda   (board_base_ptr), y
         and   #TILE_MASK
         cmp   #kWhiteTile
         beq   @inc2
         cmp   #kBlueTile
         beq   @inc3
         dey
         bpl   -
         rts
@inc2    inc   colorcount2
         dey
         bpl   -
         rts
@inc3    inc   colorcount3
         dey
         bpl   -
         rts

;------------------------------------------------------------------------------
; CountEmptyPerRow
; count number of empty tiles in every row of a game board
;
; in:    $board_base_ptr+1 = high byte of board buffer
; out:   $row_empty_counts populated
;        $row1 clobbered
;        $tile_column clobbered
;        all registers and flags clobbered
;------------------------------------------------------------------------------
CountEmptyPerRow
         ldx   #$0E
         stx   row1
         inx
         ; count number of empty tiles in each row
         ldy   #$00
-        sty   row_empty_counts, x
         dex
         bpl   -
         inx
         ;X=0
         stx   board_base_ptr
         ;Y=0
--       lda   #$10
         sta   tile_column
-        lda   (board_base_ptr), y
         bne   +
         inc   row_empty_counts, x
+        iny
         dec   tile_column
         bne   -
         inx
         tya
         bne   --
         rts
