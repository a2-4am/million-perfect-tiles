;license:MIT
;(c) 2022 by 4am
;
; a very lightweight p-code interpreter
;
; public functions:
; - TileScript
;

; opcodes (public)
EXIT     = 0
WAIT     = 1
HOME     = 2
FIZZLE   = 3
PRINT    = 4
WPRINT   = 5
FOCUS    = 6
UNFOCUS  = 7
TILE     = 8
BOARD    = 9

;------------------------------------------------------------------------------
; TileScript
; execute a series of commands
;
; in:    A/Y points to start of command list
; out:   all registers and flags clobbered
;------------------------------------------------------------------------------
TileScript
         +ST16 GetNextScriptByte+1
         jsr   HidePointer
@loop
         jsr   GetNextScriptByte
         lda   ScriptDispatchLo, x
         sta   @j+1
         lda   ScriptDispatchHi, x
         sta   @j+2
@j       jsr   $FDFD                 ; SMC
         jmp   @loop

;------------------------------------------------------------------------------
; internal functions

ScriptDispatchLo
         !byte <ScriptOnEXIT
         !byte <ScriptOnWAIT
         !byte <ScriptOnHOME
         !byte <ScriptOnFIZZLE
         !byte <ScriptOnPRINT
         !byte <ScriptOnWPRINT
         !byte <ScriptOnFOCUS
         !byte <ScriptOnUNFOCUS
         !byte <ScriptOnTILE
         !byte <ScriptOnBOARD
ScriptDispatchHi
         !byte >ScriptOnEXIT
         !byte >ScriptOnWAIT
         !byte >ScriptOnHOME
         !byte >ScriptOnFIZZLE
         !byte >ScriptOnPRINT
         !byte >ScriptOnWPRINT
         !byte >ScriptOnFOCUS
         !byte >ScriptOnUNFOCUS
         !byte >ScriptOnTILE
         !byte >ScriptOnBOARD

ScriptOnEXIT
         jsr   ShowPointer
         bit   GFXMODE
-        pla
         pla
         rts

ScriptOnWAIT
         jsr   ShowPointer
         bit   GFXMODE
         jsr   GetNextScriptByte
         jsr   LongWaitForKeyWithTimeout
         jsr   GetNextEvent
         cpx   #kEventClick
         beq   -
         cpx   #kEventKeypress
         beq   -
         jmp   HidePointer

ScriptOnHOME=Home
ScriptOnFIZZLE=GridFizzle

ScriptOnPRINT
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawCondensedString

ScriptOnWPRINT
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawLargeString

GetPrintParams
         jsr   GetNextScriptByte
         stx   VTAB
         jsr   GetNextScriptByte
         stx   HTAB
         jsr   GetNextScriptByte
         txa
         pha
         jsr   GetNextScriptByte
         txa
         tay
         pla
         ldx   HTAB
         rts

ScriptOnFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawFocusRectangleAt

ScriptOnUNFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   ClearFocusRectangleAt

ScriptOnTILE
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawLargeCharacter

ScriptOnBOARD
         jsr   GetNextScriptByte
         ; pointer is hidden
         jmp   DrawBoardAt

GetFocusParams
         jsr   GetNextScriptByte
         stx   tmpx
         jsr   GetNextScriptByte
         txa
         tay
         jsr   GetNextScriptByte
         txa
         ldx   tmpx
         rts

GetNextScriptByte
         ldx   $FDFD                 ; SMC
         inc   GetNextScriptByte+1
         bne   +
         inc   GetNextScriptByte+2
+        rts
