;license:MIT
;(c) 2023 by 4am
;
; a very lightweight p-code interpreter
;
; public functions:
; - TileScript
;

; opcodes (public)
EXIT     = 0   ; exit script
WAIT     = 1   ; wait for keypress or click, with timeout
HOME     = 2   ; clear screen
CPR      = 3   ; print condensed
WPR      = 4   ; print wide
FOCUS    = 5   ; draw focus rectangle
UNFOCUS  = 6   ; clear focus rectangle
TILE     = 7   ; draw tile
BOARD    = 8   ; draw board
CALL     = 9   ; call assembly routine

!macro CPRINT v, h, s {
         !byte CPR, v, h
         !word s
}

!macro WPRINT v, h, s {
         !byte WPR, v, h
         !word s
}

;------------------------------------------------------------------------------
; TileScript
; execute a series of commands
;
; in:    A/Y points to start of command list
; out:   all registers and flags clobbered
;------------------------------------------------------------------------------
TileScript
         +ST16 GetNextScriptByte+1
         jsr   HidePointer
@loop
         jsr   GetNextScriptByte
         lda   ScriptDispatchLo, x
         sta   @j+1
         lda   ScriptDispatchHi, x
         sta   @j+2
@j       jsr   $FDFD                 ; SMC
         jmp   @loop

;------------------------------------------------------------------------------
; private

ScriptDispatchLo
         !byte <ScriptOnEXIT
         !byte <ScriptOnWAIT
         !byte <ScriptOnHOME
         !byte <ScriptOnCPR
         !byte <ScriptOnWPR
         !byte <ScriptOnFOCUS
         !byte <ScriptOnUNFOCUS
         !byte <ScriptOnTILE
         !byte <ScriptOnBOARD
         !byte <ScriptOnCALL
ScriptDispatchHi
         !byte >ScriptOnEXIT
         !byte >ScriptOnWAIT
         !byte >ScriptOnHOME
         !byte >ScriptOnCPR
         !byte >ScriptOnWPR
         !byte >ScriptOnFOCUS
         !byte >ScriptOnUNFOCUS
         !byte >ScriptOnTILE
         !byte >ScriptOnBOARD
         !byte >ScriptOnCALL

ScriptOnEXIT
         jsr   ShowPointer
         bit   GFXMODE
-        pla
         pla
         rts

ScriptOnWAIT
         jsr   ShowPointer
         bit   GFXMODE
         jsr   GetNextScriptByte
         jsr   LongWaitForKeyWithTimeout
         jsr   GetNextEvent
         cpx   #kEventClick
         beq   -
         cpx   #kEventKeypress
         beq   -
         jmp   HidePointer

ScriptOnHOME=Home

ScriptOnCPR
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawCondensedString

ScriptOnWPR
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawLargeString

GetPrintParams
         jsr   GetNextScriptByte
         stx   VTAB
         jsr   GetNextScriptByte
         stx   HTAB
         jsr   GetNextScriptByte
         txa
         pha
         jsr   GetNextScriptByte
         txa
         tay
         pla
         ldx   HTAB
         rts

ScriptOnFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawFocusRectangleAt

ScriptOnUNFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   ClearFocusRectangleAt

ScriptOnTILE
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawLargeCharacter

GetFocusParams
         jsr   GetNextScriptByte
         stx   tmpx
         jsr   GetNextScriptByte
         txa
         tay
         jsr   GetNextScriptByte
         txa
         ldx   tmpx
         rts

ScriptOnBOARD
         jsr   GetNextScriptByte
         ; pointer is hidden
         jmp   DrawBoardAt

ScriptOnCALL
         jsr   GetNextScriptByte
         stx   @j+1
         jsr   GetNextScriptByte
         stx   @j+2
@j       jmp   $FDFD                 ; SMC

GetNextScriptByte
         ldx   $FDFD                 ; SMC
         inc   GetNextScriptByte+1
         bne   +
         inc   GetNextScriptByte+2
+        rts
