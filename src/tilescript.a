;license:MIT
;(c) 2022 by 4am
;
; a very lightweight p-code interpreter
;
; public functions:
; - TileScript
;

; opcodes (public)
EXIT     = 0
WAIT     = 1
HOME     = 2
PRINT    = 3
WPRINT   = 4
OFFPRINT = 5
FOCUS    = 6
UNFOCUS  = 7
TILE     = 8
BOARD    = 9
CALL     = 10

;------------------------------------------------------------------------------
; TileScript
; execute a series of commands
;
; in:    A/Y points to start of command list
; out:   all registers and flags clobbered
;------------------------------------------------------------------------------
TileScript
         +ST16 GetNextScriptByte+1
         jsr   HidePointer
@loop
         jsr   GetNextScriptByte
         lda   ScriptDispatchLo, x
         sta   @j+1
         lda   ScriptDispatchHi, x
         sta   @j+2
@j       jsr   $FDFD                 ; SMC
         jmp   @loop

;------------------------------------------------------------------------------
; private

ScriptDispatchLo
         !byte <ScriptOnEXIT
         !byte <ScriptOnWAIT
         !byte <ScriptOnHOME
         !byte <ScriptOnPRINT
         !byte <ScriptOnWPRINT
         !byte <ScriptOnOFFPRINT
         !byte <ScriptOnFOCUS
         !byte <ScriptOnUNFOCUS
         !byte <ScriptOnTILE
         !byte <ScriptOnBOARD
         !byte <ScriptOnCALL
ScriptDispatchHi
         !byte >ScriptOnEXIT
         !byte >ScriptOnWAIT
         !byte >ScriptOnHOME
         !byte >ScriptOnPRINT
         !byte >ScriptOnWPRINT
         !byte >ScriptOnOFFPRINT
         !byte >ScriptOnFOCUS
         !byte >ScriptOnUNFOCUS
         !byte >ScriptOnTILE
         !byte >ScriptOnBOARD
         !byte >ScriptOnCALL

ScriptOnEXIT
         jsr   ShowPointer
         bit   GFXMODE
-        pla
         pla
         rts

ScriptOnWAIT
         jsr   ShowPointer
         bit   GFXMODE
         jsr   GetNextScriptByte
         jsr   LongWaitForKeyWithTimeout
         jsr   GetNextEvent
         cpx   #kEventClick
         beq   -
         cpx   #kEventKeypress
         beq   -
         jmp   HidePointer

ScriptOnHOME=Home

ScriptOnPRINT
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawCondensedString

ScriptOnWPRINT
         jsr   GetPrintParams
         ; pointer is hidden
         jmp   DrawLargeString

         bit   $ff58
ScriptOnOFFPRINT
         jsr   GetNextScriptByte
         stx   @b+2
         jsr   GetPrintParams
         +ST16 string_ptr
         ldy   HTAB
         dey
         sty   HTAB
         ldx   VTAB
         jsr   CalculateRowBase
         clc
         adc   HTAB
         sta   @b+1
         ldy   #0
         lda   (string_ptr), y
         tay
@a       lda   (string_ptr), y
@b       sta   $FDFD, y              ; SMC
         dey
         bne   @a
         rts

GetPrintParams
         jsr   GetNextScriptByte
         stx   VTAB
         jsr   GetNextScriptByte
         stx   HTAB
         jsr   GetNextScriptByte
         txa
         pha
         jsr   GetNextScriptByte
         txa
         tay
         pla
         ldx   HTAB
         rts

ScriptOnFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawFocusRectangleAt

ScriptOnUNFOCUS
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   ClearFocusRectangleAt

ScriptOnTILE
         jsr   GetFocusParams
         ; pointer is hidden
         jmp   DrawLargeCharacter

GetFocusParams
         jsr   GetNextScriptByte
         stx   tmpx
         jsr   GetNextScriptByte
         txa
         tay
         jsr   GetNextScriptByte
         txa
         ldx   tmpx
         rts

ScriptOnBOARD
         jsr   GetNextScriptByte
         ; pointer is hidden
         jmp   DrawBoardAt

ScriptOnCALL
         jsr   GetNextScriptByte
         stx   @j+1
         jsr   GetNextScriptByte
         stx   @j+2
@j       jmp   $FDFD                 ; SMC

GetNextScriptByte
         ldx   $FDFD                 ; SMC
         inc   GetNextScriptByte+1
         bne   +
         inc   GetNextScriptByte+2
+        rts
