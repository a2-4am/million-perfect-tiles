;license:MIT
;(c) 2022 by 4am
;
; 'select size' screen
;

; Codes returned by event handlers
kStayOnSelectSize = 0
kReturnToMainMenu = 1

kSelectSizeMin = 7
kSelectSizeCount = 6
.selectSizeSelectedIndex
         !byte 7

kSelectSizeKeys                      ; must keep in sync with kSelectSizeKeyHandlersLo/Hi arrays
                                     ; except for last byte ($00) which doesn't need an associated handler
         !byte $8D                   ; Return
         !byte $8B                   ; up arrow
         !byte $88                   ; left arrow
         !byte $8A                   ; down arrow
         !byte $95                   ; right arrow
         !byte $A0                   ; space
         !byte $9B                   ; Esc
         !byte $00

kSelectSizeKeyHandlersLo
         !byte <SelectSizeEventReturn
         !byte <SelectSizeEventUp
         !byte <SelectSizeEventUp
         !byte <SelectSizeEventDown
         !byte <SelectSizeEventDown
         !byte <SelectSizeEventDown
         !byte <SelectSizeEventEsc

kSelectSizeKeyHandlersHi
         !byte >SelectSizeEventReturn
         !byte >SelectSizeEventUp
         !byte >SelectSizeEventUp
         !byte >SelectSizeEventDown
         !byte >SelectSizeEventDown
         !byte >SelectSizeEventDown
         !byte >SelectSizeEventEsc

; len(kSelectSizeVTABs) == len(kSelectSizeHTABs) == len(kSelectSizeStringsLo) == len(kSelectSizeStringsHi)
; or you will regret this
kSelectSizeVTABs
         !byte 1,2,3,4,6,7,8,9,10,11,12,13
kSelectSizeHTABs
         !byte 3,3,3,7,6,6,6,6,6 ,6 ,6 ,6
kSelectSizeStringsLo
         !byte <sSelectSizeLine1
         !byte <sSelectSizeLine2
         !byte <sSelectSizeLine3
         !byte <sSelectSizeLine4
         ; nothing on line 5
         !byte <sSelectSizeLine6
         !byte <sSelectSizeLine7
         !byte <sSelectSizeLine8
         !byte <sSelectSizeLine9
         !byte <sSelectSizeLine10
         !byte <sSelectSizeLine11
         !byte <sSelectSizeLine12
         !byte <sSelectSizeLine13
kSelectSizeStringsHi
         !byte >sSelectSizeLine1
         !byte >sSelectSizeLine2
         !byte >sSelectSizeLine3
         !byte >sSelectSizeLine4
         ; nothing on line 5
         !byte >sSelectSizeLine6
         !byte >sSelectSizeLine7
         !byte >sSelectSizeLine8
         !byte >sSelectSizeLine9
         !byte >sSelectSizeLine10
         !byte >sSelectSizeLine11
         !byte >sSelectSizeLine12
         !byte >sSelectSizeLine13

;------------------------------------------------------------------------------
; SelectSizeEventLoop
; display 'select size' menu
;
; in:    C clear if screen is already cleared and menu page is already drawn
;          (will happen if using arrow keys to move selection indicator)
;        C set if full screen clear & redraw is required
;          (will happen if user is coming from any other screen)
; out:   Z = 0
;------------------------------------------------------------------------------
SelectSizeEventLoop
         bcc   @loop
         bit   TEXTMODE
         jsr   HidePointer
         jsr   DrawSelectSizeBackground
         jsr   DrawSelectSizeText
         jsr   ShowPointer
         bit   GFXMODE
@loop
         jsr   GetNextEvent
         cpx   #kEventMouseMove
         beq   @onMouseMove
         cpx   #kEventClick
         beq   @onClick
         cpx   #kEventKeypress
         bne   @loop
         ldx   #0
-        ldy   kSelectSizeKeys, x
         beq   @loop                 ; key has no associated handler, so ignore it
         cpy   gLastKeyPressed       ; populated by WaitForKey
         beq   @dispatch
         inx
         bne   -                     ; always branches
@dispatch
         lda   kSelectSizeKeyHandlersLo, x
         sta   @j+1
         lda   kSelectSizeKeyHandlersHi, x
         sta   @j+2
@j       jsr   $FDFD                 ; SMC
         beq   SelectSizeEventLoop
         rts
@onMouseMove
         +LDADDR SelectSizeEventMouseMove
         +ST16 @j+1
         jmp   @j
@onClick
         +LDADDR SelectSizeEventClick
         +ST16 @j+1
         jmp   @j

StayOnSelectSizeNoRefresh0
         jmp   StayOnSelectSizeNoRefresh

SelectSizeEventMouseMove
         jsr   MousePositionToLogicalCoordinates
         cpx   #kSelectSizeMin
         bcc   StayOnSelectSizeNoRefresh0
         cpx   #(kSelectSizeMin + kSelectSizeCount)
         bcs   StayOnSelectSizeNoRefresh0
         cpy   #6
         bcc   StayOnSelectSizeNoRefresh0
         cpy   #10
         bcs   StayOnSelectSizeNoRefresh0
         cpx   .selectSizeSelectedIndex
         beq   StayOnSelectSizeNoRefresh0
         txa
         pha
         jsr   HidePointer
         jsr   EraseSelectSizeSelectionIndicator
         pla
         tax
         jmp   .SelectSizeRedrawSelect

SelectSizeEventClick
         jsr   ClickPositionToLogicalCoordinates
         cpx   #kSelectSizeMin
         bcc   StayOnSelectSizeNoRefresh0
         cpx   #(kSelectSizeMin + kSelectSizeCount)
         bcs   StayOnSelectSizeNoRefresh0
         cpy   #6
         bcc   StayOnSelectSizeNoRefresh0
         cpy   #10
         bcs   StayOnSelectSizeNoRefresh0
         stx   .selectSizeSelectedIndex
         ; /!\ execution falls through here to SelectSizeEventReturn

SelectSizeEventReturn
         jsr   Home
         lda   .selectSizeSelectedIndex
         sec
         sbc   #(kSelectSizeMin-3)
         asl
         sta   logical_board_size
         sta   gLastSelectedBoardSize
         lda   RNDSEED+1
         and   #1
         tay
         lda   RNDSEED
         +ST16 gLastSelectedBoardIndex
ReloadBoard
         ldx   #>kUserBoardBuffer
         jsr   nth
         jsr   HidePointer
         jsr   DrawBoardForUser
         jsr   FindEmptyCoordinatesForUser
         stx   gFocusedRow
         sty   gFocusedColumn
         jsr   DrawFocusRectangle
         bit   GFXMODE
         jsr   ShowPointer
         jsr   PlayEventLoop
         ; TODO maybe another dispatch table here?
         cpx   #kRequestedSkip
         beq   SelectSizeEventReturn
         cpx   #kRequestedRestart
         bne   +
         jsr   Home
         lda   gLastSelectedBoardSize
         sta   logical_board_size
         +LD16 gLastSelectedBoardIndex
         jmp   ReloadBoard
+        cpx   #kReturnToSelectSize
         bne   +
         ldx   #kStayOnSelectSize
         sec
         rts
+
         ; TODO completed puzzle
         brk

SelectSizeEventUp
         jsr   HidePointer
         jsr   EraseSelectSizeSelectionIndicator
         dec   .selectSizeSelectedIndex
         ldx   .selectSizeSelectedIndex
         cpx   #kSelectSizeMin
         bcs   +
         ldx   #(kSelectSizeMin+kSelectSizeCount-1)
+        jmp   .SelectSizeRedrawSelect

SelectSizeEventDown
         jsr   HidePointer
         jsr   EraseSelectSizeSelectionIndicator
         inc   .selectSizeSelectedIndex
         ldx   .selectSizeSelectedIndex
         cpx   #(kSelectSizeMin+kSelectSizeCount)
         bcc   .SelectSizeRedrawSelect
         ldx   #kSelectSizeMin
.SelectSizeRedrawSelect
         jsr   SetSelectSizeSelectionIndexAndRedraw
         jsr   ShowPointer
StayOnSelectSizeNoRefresh
         ldx   #kStayOnSelectSize    ; Z=1 so caller will stay in select size event loop
         clc
         rts

SelectSizeEventEsc
         ldx   #kReturnToMainMenu
         rts

DrawSelectSizeBackground
         jmp   DrawBoardForTitle

DrawSelectSizeText
         lda   #11
         sta   row1
-        ldy   row1
         lda   kSelectSizeVTABs, y
         sta   VTAB
         lda   kSelectSizeHTABs, y
         tax
         lda   kSelectSizeStringsLo, y
         pha
         lda   kSelectSizeStringsHi, y
         tay
         pla
         jsr   DrawLargeString
         dec   row1
         bpl   -
         ldx   .selectSizeSelectedIndex
         ; /!\ execution falls through here to DrawSelectSizeSelectionIndicator

SetSelectSizeSelectionIndexAndRedraw
; in:    X = new selection index
         stx   .selectSizeSelectedIndex
         ldy   kSelectSizeHTABs+8
         lda   #$01                  ; selection indicator character
         jmp   DrawLargeCharacter

EraseSelectSizeSelectionIndicator
         ldx   .selectSizeSelectedIndex
         ldy   kSelectSizeHTABs+8
         lda   #$00                  ; blank character
         jmp   DrawLargeCharacter
