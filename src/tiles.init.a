;license:MIT
;(c) 2022 by 4am
;
; initialize program, generate lookup tables and dynamically generated code
;

         ldx   #$FF
         txs
         +READ_ROM_NO_WRITE
         sta   PRIMARYCHARSET
         sta   CLR80VID
         sta   STOREOFF
         sta   READMAINMEM
         sta   WRITEMAINMEM
         jsr   ROM_TEXT
         jsr   ROM_HOME
         jsr   ROM_NORMAL
         jsr   ROM_IN0
         jsr   ROM_PR0

         +BUILD_HGR_LOOKUP_TABLES HGRLO, HGRHI

         ; build EnableAccelerator and DisableAccelerator functions
         ; and copy them into place
         jsr   BuildAcceleratorFunction
         +ST16 @accelSrc
         dex
-
@accelSrc=*+1
         lda   $FDFD, x
         sta   DisableAccelerator, x
         dex
         bpl   -

         ; relocate rest of program code to $4000
-        inc   FM+2
         inc   FMDEST+2
         lda   FM+2
         cmp   #>LastMover
         bcc   -
         ldx   #$00
FM       lda   FirstMover, x
FMDEST   sta   $4000, x
         inx
         bne   FM
         dec   FM+2
         dec   FMDEST+2
         lda   FMDEST+2
         cmp   #$40
         bcs   FM
         ;X=0
         ; set up some globals used by FindValidMove
         stx   start_y
         dex
         stx   board_base_shift_minus_1
         stx   maybe_xx0
         stx   maybe_x0x
         dex
         stx   board_base_shift_minus_2
         ldx   #1
         stx   board_base_shift_plus_1
         inx
         stx   board_base_shift_plus_2
         ldy   #$00
--       ldx   #$0E
         lda   #$00
-        sta   maybe_0xx, y
         iny
         dex
         bne   -
         lda   #$FF
         sta   maybe_0xx, y
         iny
         sta   maybe_0xx, y
         iny
         bne   --

         jsr   BuildVBLFunction

         ; load random board for title (out of first 512 16x16 boards)
         lda   #kTitleBoardSize
         sta   logical_board_size
         lda   RNDSEED+1
         and   #1
         tay
         lda   RNDSEED
         ldx   #>kTitleBoardBuffer
         jsr   nth

         ; disable accelerator for Mockingboard detection (required)
         ; but also leave it disabled for duration of play
         jsr   DisableAccelerator

         jsr   mockingboard_detect
         ;clc   ; DEBUG uncomment to pretend there's no Mockingboard
         php
         ror   gMockingboardAvailable
         plp
         bcc   +
         jsr   mockingboard_patch               ; self-modify Mockingboard code for slot independence
         jsr   pt3_init_song                    ; prepare song data
+
         jmp   Start

         !source "src/hw.vbl.init.a"
