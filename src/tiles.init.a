;license:MIT
;(c) 2022 by 4am
;
; initialize program, generate lookup tables and dynamically generated code
;

         ldx   #$FF
         txs
         +READ_ROM_NO_WRITE
         sta   PRIMARYCHARSET
         sta   CLR80VID
         sta   STOREOFF
         sta   READMAINMEM
         sta   WRITEMAINMEM
         jsr   ROM_TEXT
         jsr   ROM_HOME
         jsr   ROM_NORMAL
         jsr   ROM_IN0
         jsr   ROM_PR0

         +BUILD_HGR_LOOKUP_TABLES HGRLO, HGRHI

         ; build EnableAccelerator and DisableAccelerator functions
         ; and copy them into place
         jsr   BuildAcceleratorFunction
         +ST16 @accelSrc
         dex
-
@accelSrc=*+1
         lda   $FDFD, x
         sta   DisableAccelerator, x
         dex
         bpl   -

         ; relocate music to $800
         ldy   #5                               ; hardcoded to 0x500 bytes
         ldx   #0
MSRC     lda   music_raw, x
MDST     sta   PT3_LOC, x
         inx
         bne   MSRC
         inc   MSRC+2
         inc   MDST+2
         dey
         bne   MSRC

         ; relocate rest of program code to $4000
-        inc   FM+2
         inc   FMDEST+2
         lda   FM+2
         cmp   #>LastMover
         bcc   -
         ;X=0
FM       lda   FirstMover, x
FMDEST   sta   $4000, x
         inx
         bne   FM
         dec   FM+2
         dec   FMDEST+2
         lda   FMDEST+2
         cmp   #$40
         bcs   FM

         ; set up some globals used by FindValidMove
         ;X=0
         stx   start_y
         dex
         stx   board_base_shift_minus_1
         stx   maybe_xx0
         stx   maybe_x0x
         dex
         stx   board_base_shift_minus_2
         ldx   #1
         stx   board_base_shift_plus_1
         inx
         stx   board_base_shift_plus_2
         ldy   #$00
--       ldx   #$0E
         lda   #$00
-        sta   maybe_0xx, y
         iny
         dex
         bne   -
         lda   #$FF
         sta   maybe_0xx, y
         iny
         sta   maybe_0xx, y
         iny
         bne   --

         ; build model-specific VBL code (used for drawing pointer)
         jsr   BuildVBLFunction

         ; load random board for title (out of first 512 16x16 boards)
         jsr   MLI
         !byte $82                   ; GET_TIME
         !word 0
         lda   $BF93
         and   #%00000001
         tay
         lda   $BF93
         and   #%11111110
         eor   $BF92
         eor   $BF91
         eor   $BF90
         ldx   #kTitleBoardSize
         stx   logical_board_size
         ldx   #>kTitleBoardBuffer
         jsr   nth

         ; disable accelerator for Mockingboard detection (required)
         ; but also leave it disabled for duration of play
         jsr   DisableAccelerator

         ; now safe to detect Mockingboard
         jsr   mockingboard_detect
         ;clc                                   ; DEBUG uncomment to pretend there's no Mockingboard
         php
         ror   gMockingboardAvailable
         plp
         bcc   +
         jsr   mockingboard_patch               ; self-modify Mockingboard code for slot independence
         jsr   pt3_init_song                    ; prepare song data
+

         jmp   Start

         !source "src/hw.vbl.init.a"
!ifdef PT3_ENABLE_APPLE_IIC {
         !source "src/pt3/pt3_lib_detect_model.s"
}
         !source "src/pt3/pt3_lib_mockingboard_detect.s"
         !source "src/pt3/pt3_lib_init.s"
music_raw
         !binary "res/SMALLBLUES.PT3"
         !byte 40                               ; ends up as the length prefix for gCurrentMessage
